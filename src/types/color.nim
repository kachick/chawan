import std/algorithm
import std/options
import std/strutils
import std/tables

import utils/twtstr

type
  RGBColor* = distinct uint32

  # ARGB color. machine-dependent format, so that bit shifts and arithmetic
  # works. (Alpha is MSB, then come R, G, B.)
  ARGBColor* = distinct uint32

  # RGBA format; machine-independent, always big-endian.
  RGBAColorBE* {.packed.} = object
    r*: uint8
    g*: uint8
    b*: uint8
    a*: uint8

  ANSIColor* = distinct uint8

  EightBitColor* = distinct uint8

  # ctNone: default color (intentionally 0), n is unused
  # ctANSI: ANSI color, as selected by SGR 38/48
  # ctRGB: RGB color
  ColorTag* = enum
    ctNone, ctANSI, ctRGB

  CellColor* = object
    t*: ColorTag
    n: uint32

func rgba*(r, g, b, a: uint8): ARGBColor

func toRGBColor*(i: ARGBColor): RGBColor =
  return RGBColor(uint32(i) and 0xFFFFFFu32)

converter toARGBColor*(i: RGBColor): ARGBColor =
  return ARGBColor(uint32(i) or 0xFF000000u32)

converter toARGBColor*(c: RGBAColorBE): ARGBColor =
  return rgba(c.r, c.g, c.b, c.a)

func `==`*(a, b: ARGBColor): bool {.borrow.}

func `==`*(a, b: ANSIColor): bool {.borrow.}

func `==`*(a, b: EightBitColor): bool {.borrow.}

func rgbcolor*(color: CellColor): RGBColor =
  cast[RGBColor](color.n)

func argbcolor*(color: CellColor): ARGBColor =
  cast[ARGBColor](color.n)

func color*(color: CellColor): uint8 =
  uint8(color.n)

func eightbit*(color: CellColor): EightBitColor =
  EightBitColor(color.color)

func cellColor*(rgb: RGBColor): CellColor =
  return CellColor(t: ctRGB, n: uint32(rgb) or 0xFF000000u32)

func cellColor*(rgba: ARGBColor): CellColor =
  return CellColor(t: ctRGB, n: uint32(rgba))

#TODO bright ANSI colors (8..15)

func cellColor*(c: ANSIColor): CellColor =
  return CellColor(t: ctANSI, n: uint32(c))

func cellColor*(c: EightBitColor): CellColor =
  return CellColor(t: ctANSI, n: uint32(c))

const defaultColor* = CellColor(t: ctNone, n: 0)

const ColorsRGBMap = {
  "aliceblue": 0xF0F8FFu32,
  "antiquewhite": 0xFAEBD7u32,
  "aqua": 0x00FFFFu32,
  "aquamarine": 0x7FFFD4u32,
  "azure": 0xF0FFFFu32,
  "beige": 0xF5F5DCu32,
  "bisque": 0xFFE4C4u32,
  "black": 0x000000u32,
  "blanchedalmond": 0xFFEBCDu32,
  "blue": 0x0000FFu32,
  "blueviolet": 0x8A2BE2u32,
  "brown": 0xA52A2Au32,
  "burlywood": 0xDEB887u32,
  "cadetblue": 0x5F9EA0u32,
  "chartreuse": 0x7FFF00u32,
  "chocolate": 0xD2691Eu32,
  "coral": 0xFF7F50u32,
  "cornflowerblue": 0x6495EDu32,
  "cornsilk": 0xFFF8DCu32,
  "crimson": 0xDC143Cu32,
  "cyan": 0x00FFFFu32,
  "darkblue": 0x00008Bu32,
  "darkcyan": 0x008B8Bu32,
  "darkgoldenrod": 0xB8860Bu32,
  "darkgray": 0xA9A9A9u32,
  "darkgreen": 0x006400u32,
  "darkgrey": 0xA9A9A9u32,
  "darkkhaki": 0xBDB76Bu32,
  "darkmagenta": 0x8B008Bu32,
  "darkolivegreen": 0x556B2Fu32,
  "darkorange": 0xFF8C00u32,
  "darkorchid": 0x9932CCu32,
  "darkred": 0x8B0000u32,
  "darksalmon": 0xE9967Au32,
  "darkseagreen": 0x8FBC8Fu32,
  "darkslateblue": 0x483D8Bu32,
  "darkslategray": 0x2F4F4Fu32,
  "darkslategrey": 0x2F4F4Fu32,
  "darkturquoise": 0x00CED1u32,
  "darkviolet": 0x9400D3u32,
  "deeppink": 0xFF1493u32,
  "deepskyblue": 0x00BFFFu32,
  "dimgray": 0x696969u32,
  "dimgrey": 0x696969u32,
  "dodgerblue": 0x1E90FFu32,
  "firebrick": 0xB22222u32,
  "floralwhite": 0xFFFAF0u32,
  "forestgreen": 0x228B22u32,
  "fuchsia": 0xFF00FFu32,
  "gainsboro": 0xDCDCDCu32,
  "ghostwhite": 0xF8F8FFu32,
  "gold": 0xFFD700u32,
  "goldenrod": 0xDAA520u32,
  "gray": 0x808080u32,
  "green": 0x008000u32,
  "greenyellow": 0xADFF2Fu32,
  "grey": 0x808080u32,
  "honeydew": 0xF0FFF0u32,
  "hotpink": 0xFF69B4u32,
  "indianred": 0xCD5C5Cu32,
  "indigo": 0x4B0082u32,
  "ivory": 0xFFFFF0u32,
  "khaki": 0xF0E68Cu32,
  "lavender": 0xE6E6FAu32,
  "lavenderblush": 0xFFF0F5u32,
  "lawngreen": 0x7CFC00u32,
  "lemonchiffon": 0xFFFACDu32,
  "lightblue": 0xADD8E6u32,
  "lightcoral": 0xF08080u32,
  "lightcyan": 0xE0FFFFu32,
  "lightgoldenrodyellow": 0xFAFAD2u32,
  "lightgray": 0xD3D3D3u32,
  "lightgreen": 0x90EE90u32,
  "lightgrey": 0xD3D3D3u32,
  "lightpink": 0xFFB6C1u32,
  "lightsalmon": 0xFFA07Au32,
  "lightseagreen": 0x20B2AAu32,
  "lightskyblue": 0x87CEFAu32,
  "lightslategray": 0x778899u32,
  "lightslategrey": 0x778899u32,
  "lightsteelblue": 0xB0C4DEu32,
  "lightyellow": 0xFFFFE0u32,
  "lime": 0x00FF00u32,
  "limegreen": 0x32CD32u32,
  "linen": 0xFAF0E6u32,
  "magenta": 0xFF00FFu32,
  "maroon": 0x800000u32,
  "mediumaquamarine": 0x66CDAAu32,
  "mediumblue": 0x0000CDu32,
  "mediumorchid": 0xBA55D3u32,
  "mediumpurple": 0x9370DBu32,
  "mediumseagreen": 0x3CB371u32,
  "mediumslateblue": 0x7B68EEu32,
  "mediumspringgreen": 0x00FA9Au32,
  "mediumturquoise": 0x48D1CCu32,
  "mediumvioletred": 0xC71585u32,
  "midnightblue": 0x191970u32,
  "mintcream": 0xF5FFFAu32,
  "mistyrose": 0xFFE4E1u32,
  "moccasin": 0xFFE4B5u32,
  "navajowhite": 0xFFDEADu32,
  "navy": 0x000080u32,
  "oldlace": 0xFDF5E6u32,
  "olive": 0x808000u32,
  "olivedrab": 0x6B8E23u32,
  "orange": 0xFFA500u32,
  "orangered": 0xFF4500u32,
  "orchid": 0xDA70D6u32,
  "palegoldenrod": 0xEEE8AAu32,
  "palegreen": 0x98FB98u32,
  "paleturquoise": 0xAFEEEEu32,
  "palevioletred": 0xDB7093u32,
  "papayawhip": 0xFFEFD5u32,
  "peachpuff": 0xFFDAB9u32,
  "peru": 0xCD853Fu32,
  "pink": 0xFFC0CBu32,
  "plum": 0xDDA0DDu32,
  "powderblue": 0xB0E0E6u32,
  "purple": 0x800080u32,
  "rebeccapurple": 0x663399u32,
  "red": 0xFF0000u32,
  "rosybrown": 0xBC8F8Fu32,
  "royalblue": 0x4169E1u32,
  "saddlebrown": 0x8B4513u32,
  "salmon": 0xFA8072u32,
  "sandybrown": 0xF4A460u32,
  "seagreen": 0x2E8B57u32,
  "seashell": 0xFFF5EEu32,
  "sienna": 0xA0522Du32,
  "silver": 0xC0C0C0u32,
  "skyblue": 0x87CEEBu32,
  "slateblue": 0x6A5ACDu32,
  "slategray": 0x708090u32,
  "slategrey": 0x708090u32,
  "snow": 0xFFFAFAu32,
  "springgreen": 0x00FF7Fu32,
  "steelblue": 0x4682B4u32,
  "tan": 0xD2B48Cu32,
  "teal": 0x008080u32,
  "thistle": 0xD8BFD8u32,
  "tomato": 0xFF6347u32,
  "turquoise": 0x40E0D0u32,
  "violet": 0xEE82EEu32,
  "wheat": 0xF5DEB3u32,
  "white": 0xFFFFFFu32,
  "whitesmoke": 0xF5F5F5u32,
  "yellow": 0xFFFF00u32,
  "yellowgreen": 0x9ACD32u32,
}

func namedRGBColor*(s: string): Option[RGBColor] =
  let i = ColorsRGBMap.binarySearch(s,
    proc(x: (string, uint32); y: string): int =
      return x[0].cmpIgnoreCase(y)
  )
  if i != -1:
    return some(RGBColor(ColorsRGBMap[i][1]))
  return none(RGBColor)

func r*(c: ARGBColor): uint8 =
  return uint8(uint32(c) shr 16)

func g*(c: ARGBColor): uint8 =
  return uint8(uint32(c) shr 8)

func b*(c: ARGBColor): uint8 =
  return uint8(uint32(c))

func a*(c: ARGBColor): uint8 =
  return uint8(uint32(c) shr 24)

proc `r=`*(c: var ARGBColor, r: uint8) =
  c = ARGBColor(uint32(c) or (uint32(r) shl 16))

proc `g=`*(c: var ARGBColor, g: uint8) =
  c = ARGBColor(uint32(c) or (uint32(g) shl 8))

proc `b=`*(c: var ARGBColor, b: uint8) =
  c = ARGBColor(uint32(c) or uint32(b))

proc `a=`*(c: var ARGBColor, a: uint8) =
  c = ARGBColor(uint32(c) or (uint32(a) shl 24))

# https://html.spec.whatwg.org/#serialisation-of-a-color
func serialize*(color: ARGBColor): string =
  if color.a == 255:
    var res = "#"
    res.pushHex(color.r)
    res.pushHex(color.g)
    res.pushHex(color.b)
    return res
  let a = float64(color.a) / 255
  return "rgba(" & $color.r & ", " & $color.g & ", " & $color.b & ", " & $a &
    ")"

func `$`*(argbcolor: ARGBColor): string =
  return argbcolor.serialize()

# https://arxiv.org/pdf/2202.02864.pdf
func fastmul*(c, ca: uint32): uint32 =
  let u = c or 0xFF000000u32
  var rb = u and 0x00FF00FFu32
  rb *= ca
  rb += 0x00800080
  rb += (rb shr 8) and 0x00FF00FFu32
  rb = rb and 0xFF00FF00u32
  var ga = (u shr 8) and 0x00FF00FFu32
  ga *= ca
  ga += 0x00800080
  ga += (ga shr 8) and 0x00FF00FFu32
  ga = ga and 0xFF00FF00u32
  return ga or (rb shr 8)

# fastmul, but preserves alpha
func fastmul1(c, ca: uint32): uint32 =
  let u = c
  var rb = u and 0x00FF00FFu32
  rb *= ca
  rb += 0x00800080
  rb += (rb shr 8) and 0x00FF00FFu32
  rb = rb and 0xFF00FF00u32
  var ga = (u shr 8) and 0x00FF00FFu32
  ga *= ca
  ga += 0x00800080
  ga += (ga shr 8) and 0x00FF00FFu32
  ga = ga and 0xFF00FF00u32
  return ga or (rb shr 8)

func fastmul1(c: ARGBColor; ca: uint32): ARGBColor =
  return ARGBColor(fastmul1(uint32(c), ca))

func premul(c: ARGBColor): ARGBColor =
  return ARGBColor(fastmul(uint32(c), uint32(c.a)))

# This is somewhat faster than floats or a lookup table, and is correct for
# all inputs.
proc straight(c: ARGBColor): ARGBColor =
  let a8 = c.a
  if a8 == 0:
    return ARGBColor(0)
  let a = uint32(a8)
  let r = ((uint32(c.r) * 0xFF00 div a + 0x80) shr 8) and 0xFF
  let g = ((uint32(c.g) * 0xFF00 div a + 0x80) shr 8) and 0xFF
  let b = ((uint32(c.b) * 0xFF00 div a + 0x80) shr 8) and 0xFF
  return ARGBColor((a shl 24) or (r shl 16) or (g shl 8) or b)

func blend*(c0, c1: ARGBColor): ARGBColor =
  let pc0 = c0.premul()
  let pc1 = c1.premul()
  let k = 255 - pc1.a
  let mc = fastmul1(pc0, uint32(k))
  let rr = pc1.r + mc.r
  let rg = pc1.g + mc.g
  let rb = pc1.b + mc.b
  let ra = pc1.a + mc.a
  let pres = rgba(rr, rg, rb, ra)
  let res = straight(pres)
  return res

func rgb*(r, g, b: uint8): RGBColor =
  return RGBColor((uint32(r) shl 16) or (uint32(g) shl 8) or uint32(b))

func r*(c: RGBColor): uint8 =
  return uint8(uint32(c) shr 16)

func g*(c: RGBColor): uint8 =
  return uint8(uint32(c) shr 8)

func b*(c: RGBColor): uint8 =
  return uint8(uint32(c))

# see https://learn.microsoft.com/en-us/previous-versions/windows/embedded/ms893078(v=msdn.10)
func Y*(c: RGBColor): uint8 =
  let rmul = uint16(c.r) * 66u16
  let gmul = uint16(c.g) * 129u16
  let bmul = uint16(c.b) * 25u16
  return uint8(((rmul + gmul + bmul + 128) shr 8) + 16)

func U*(c: RGBColor): uint8 =
  let rmul = uint16(c.r) * 38u16
  let gmul = uint16(c.g) * 74u16
  let bmul = uint16(c.b) * 112u16
  return uint8(((128 + bmul - rmul - gmul) shr 8) + 128)

func V*(c: RGBColor): uint8 =
  let rmul = uint16(c.r) * 112u16
  let gmul = uint16(c.g) * 94u16
  let bmul = uint16(c.b) * 18u16
  return uint8(((128 + rmul - gmul - bmul) shr 8) + 128)

func YUV*(Y, U, V: uint8): RGBColor =
  let C = int(Y) - 16
  let D = int(U) - 128
  let E = int(V) - 128
  let r = max(min((298 * C + 409 * E + 128) shr 8, 255), 0)
  let g = max(min((298 * C - 100 * D - 208 * E + 128) shr 8, 255), 0)
  let b = max(min((298 * C + 516 * D + 128) shr 8, 255), 0)
  return rgb(uint8(r), uint8(g), uint8(b))

func rgba*(r, g, b, a: uint8): ARGBColor =
  return ARGBColor((uint32(a) shl 24) or (uint32(r) shl 16) or
    (uint32(g) shl 8) or uint32(b))

func rgba_be*(r, g, b, a: uint8): RGBAColorBE =
  return RGBAColorBE(r: r, g: g, b: b, a: a)

func rgb_be*(r, g, b: uint8): RGBAColorBE =
  return RGBAColorBE(r: r, g: g, b: b, a: 0xFF)

func rgba*(r, g, b, a: int): ARGBColor =
  return rgba(uint8(r), uint8(g), uint8(b), uint8(a))

func gray*(n: uint8): RGBColor =
  return rgb(n, n, n)

func gray_be*(n: uint8): RGBAColorBE =
  return rgb_be(n, n, n)

# NOTE: this assumes n notin 0..15 (which would be ANSI 4-bit)
func toRGB*(param0: EightBitColor): RGBColor =
  doAssert uint8(param0) notin 0u8..15u8
  let u = uint8(param0)
  if u in 16u8..231u8:
    #16 + 36 * r + 6 * g + b
    let n = u - 16
    let r = uint8(int(n div 36) * 255 div 5)
    let m = int(n mod 36)
    let g = uint8(((m div 6) * 255) div 5)
    let b = uint8(((m mod 6) * 255) div 5)
    return rgb(r, g, b)
  else: # 232..255
    let n = (u - 232) * 10 + 8
    return gray(n)

func toEightBit(r, g, b: uint8): EightBitColor =
  let r = int(r)
  let g = int(g)
  let b = int(b)
  # Idea from here: https://github.com/Qix-/color-convert/pull/75
  # This seems to work about as well as checking for
  # abs(U - 128) < 5 & abs(V - 128 < 5), but is definitely faster.
  if r shr 4 == g shr 4 and g shr 4 == b shr 4:
    if r < 8:
      return EightBitColor(16)
    if r > 248:
      return EightBitColor(231)
    return EightBitColor(uint8(((r - 8) * 24 div 247) + 232))
  #16 + 36 * r + 6 * g + b
  return EightBitColor(uint8(16 + 36 * (r * 5 div 255) +
    6 * (g * 5 div 255) + (b * 5 div 255)))

func toEightBit*(c: RGBColor): EightBitColor =
  return toEightBit(c.r, c.g, c.b)

func toEightBit*(c: RGBAColorBE): EightBitColor =
  return toEightBit(c.r, c.g, c.b)

template `$`*(rgbcolor: RGBColor): string =
  "rgb(" & $rgbcolor.r & ", " & $rgbcolor.g & ", " & $rgbcolor.b & ")"

template `$`*(color: CellColor): string =
  case color.t
  of ctNone: "none"
  of ctRGB: $color.argbcolor
  of ctANSI: "ansi" & $color.n

func parseHexColor*(s: openArray[char]): Option[ARGBColor] =
  for c in s:
    if c notin AsciiHexDigit:
      return none(ARGBColor)
  case s.len
  of 6:
    let c = 0xFF000000 or
      (hexValue(s[0]) shl 20) or (hexValue(s[1]) shl 16) or
      (hexValue(s[2]) shl 12) or (hexValue(s[3]) shl 8) or
      (hexValue(s[4]) shl 4) or hexValue(s[5])
    return some(ARGBColor(c))
  of 8:
    let c = (hexValue(s[6]) shl 28) or (hexValue(s[7]) shl 24) or
      (hexValue(s[0]) shl 20) or (hexValue(s[1]) shl 16) or
      (hexValue(s[2]) shl 12) or (hexValue(s[3]) shl 8) or
      (hexValue(s[4]) shl 4) or hexValue(s[5])
    return some(ARGBColor(c))
  of 3:
    let c = 0xFF000000 or
      (hexValue(s[0]) shl 20) or (hexValue(s[0]) shl 16) or
      (hexValue(s[1]) shl 12) or (hexValue(s[1]) shl 8) or
      (hexValue(s[2]) shl 4) or hexValue(s[2])
    return some(ARGBColor(c))
  of 4:
    let c = (hexValue(s[3]) shl 28) or (hexValue(s[3]) shl 24) or
      (hexValue(s[0]) shl 20) or (hexValue(s[0]) shl 16) or
      (hexValue(s[1]) shl 12) or (hexValue(s[1]) shl 8) or
      (hexValue(s[2]) shl 4) or hexValue(s[2])
    return some(ARGBColor(c))
  else:
    return none(ARGBColor)

func parseARGBColor*(s: string): Option[ARGBColor] =
  if (let x = namedRGBColor(s); x.isSome):
    return some(x.get.toARGBColor())
  if (s.len == 3 or s.len == 4 or s.len == 6 or s.len == 8) and s[0] == '#':
    return parseHexColor(s.toOpenArray(1, s.high))
  if s.len > 2 and s[0] == '0' and s[1] == 'x':
    return parseHexColor(s.toOpenArray(2, s.high))
  return parseHexColor(s)

func myHexValue(c: char): uint32 =
  let n = hexValue(c)
  if n != -1:
    return uint32(n)
  return 0

func parseLegacyColor0*(s: string): RGBColor =
  assert s != ""
  if (let x = namedRGBColor(s); x.isSome):
    return x.get
  if s.find(AllChars - AsciiHexDigit) == -1:
    let c = (hexValue(s[0]) * 17 shl 16) or
      (hexValue(s[1]) * 17 shl 8) or
      (hexValue(s[2]) * 17)
    return RGBColor(c)
  # o_0
  var s2 = if s[0] == '#':
    s.substr(1)
  else:
    s
  while s2.len == 0 or s2.len mod 3 != 0:
    s2 &= '0'
  let l = s2.len div 3
  let c = if l == 1:
    (myHexValue(s2[0]) shl 20) or (myHexValue(s2[0]) shl 16) or
    (myHexValue(s2[1]) shl 12) or (myHexValue(s2[1]) shl 8) or
    (myHexValue(s2[2]) shl 4) or myHexValue(s2[2])
  else:
    (myHexValue(s2[0]) shl 20) or (myHexValue(s2[1]) shl 16) or
    (myHexValue(s2[l]) shl 12) or (myHexValue(s2[l + 1]) shl 8) or
    (myHexValue(s2[l * 2]) shl 4) or myHexValue(s2[l * 2 + 1])
  return RGBColor(c)
